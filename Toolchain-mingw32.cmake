set(CMAKE_SYSTEM_NAME Windows)

set(MINGW_TRIPLE_DEFAULT "i686-w64-mingw32")
set(MINGW_TRIPLE ${MINGW_TRIPLE_DEFAULT} CACHE STRING "Target triple of MinGW compiler. Default: ${MINGW_TRIPLE_DEFAULT}.")
set(MINGW_PREFIX "/usr/${MINGW_TRIPLE}" CACHE STRING "Prefix of MinGW installation. Default: /usr/${MINGW_TRIPLE}.")

if("${MINGW_TRIPLE}" STREQUAL "i686-w64-mingw32")
    set(CMAKE_SYSTEM_PROCESSOR X86)
elseif("${MINGW_TRIPLE}" STREQUAL "x86_64-w64-mingw32")
    set(CMAKE_SYSTEM_PROCESSOR AMD64)
else()
    message(FATAL_ERROR "Unknown MinGW target ${MINGW_TRIPLE}. Cannot set processor type.")
endif()

set(CMAKE_C_COMPILER "${MINGW_TRIPLE}-gcc")
set(CMAKE_CXX_COMPILER "${MINGW_TRIPLE}-g++")
set(CMAKE_RC_COMPILER "${MINGW_TRIPLE}-windres")

set(CMAKE_FIND_ROOT_PATH ${MINGW_PREFIX})

set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)

set(ENV{PKG_CONFIG_DIR} "")
set(ENV{PKG_CONFIG_LIBDIR} "${MINGW_PREFIX}/lib/pkgconfig")
